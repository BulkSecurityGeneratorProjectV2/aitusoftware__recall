def recallVersion = file('version.txt').text.trim()

subprojects {
    version = recallVersion

    apply plugin: 'java-library'

    test {
        useJUnitPlatform()
    }

    repositories {
        mavenCentral()
    }
}

project(':recall-store') {
    dependencies {
        compile 'org.agrona:agrona:0.9.28'
        testCompile group: 'com.google.truth', name: 'truth', version: '0.42'
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.2'
        testCompile 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    }
}

project(':recall-sbe') {
    dependencies {
        compile group: 'uk.co.real-logic', name: 'sbe-all', version: '1.10.2'
        compile project(':recall-store')
        compile 'org.agrona:agrona:0.9.28'
        testCompile group: 'com.google.truth', name: 'truth', version: '0.42'
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.2'
        testCompile 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    }

    File generatedSrcDir = file("${buildDir}/generated-sources/java")

    sourceSets {
        main.java.srcDir generatedSrcDir
    }

    test.dependsOn('generateSbeFiles')

    task generateSbeFiles(type: JavaExec) {
        main 'uk.co.real_logic.sbe.SbeTool'
        classpath sourceSets.main.runtimeClasspath
        jvmArgs = ["-Dsbe.output.dir=${generatedSrcDir.getAbsolutePath()}", '-Dsbe.generate.ir=true', '-Dsbe.java.generate.interfaces=true']
        args "${project.projectDir}/src/test/resources/sbe-schema.xml"
    }
}

/*
project(':recall-annotations') {

}
*/
